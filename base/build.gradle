apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.1"
group = "com.droidcba.core"

def siteUrl = 'https://github.com/juanchosaravia/android_base_module'
def gitUrl = 'https://github.com/juanchosaravia/android_base_module.git'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:22.2.+'
    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'com.android.support:design:22.2.+'

    // Icepick: for automatic save/restore variables
    compile 'frankiesardo:icepick:3.0.2'
    provided 'frankiesardo:icepick-processor:3.0.2'

    // Butterknife
    compile 'com.jakewharton:butterknife:6.1.0'
}


// *****************
// Bintray Config:

def userVal = ""
def keyVal = ""

try {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream());
    userVal = properties.getProperty("bintray.user")
    keyVal = properties.getProperty("bintray.apikey")
} catch (all) {
    userVal = System.getenv('bintray.user')
    keyVal = System.getenv('bintray.apikey')
}



bintray {
    user = userVal
    key = keyVal

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = 'android_base_module'
        desc = 'Base classes to start an Android Project'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        labels = ['android', 'core', 'base']
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Base lib for Android. '
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'juanchosaravia'
                        name 'Juancho Saravia'
                        email 'juanchosaravia@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}